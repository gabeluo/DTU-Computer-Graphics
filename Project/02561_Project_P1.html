<!DOCTYPE html>
<html>
<head>
	<title>02561 Project Part 1</title>
	<script type="text/javascript" src="../Common/webgl-utils.js"></script>
	<script type="text/javascript" src="../Common/initShaders.js"></script>
	<script type="text/javascript" src="../Common/MV.js"></script>
	<script type="text/javascript" src="../Common/OBJParser.js"> </script>
	<script type="text/javascript" src="02561_Project_P1.js"></script>
	
	<script id="shadow-vertex-shader" type="x-shader/x-vertex">
		attribute vec4 a_Position;
		attribute vec4 a_Normal;
		attribute vec4 a_Color;

		uniform mat4 u_Model;
		uniform mat4 u_Perspective;
		uniform mat4 u_View;

		varying vec4 v_Normal;
		varying vec4 v_Color;

		void main()
		{
			gl_Position = u_Perspective * u_View * u_Model * a_Position;
			v_Normal = a_Normal;
			v_Color = a_Color;
		}
	</script>
	
	<script id="shadow-fragment-shader" type="x-shader/x-fragment">
		precision highp float;
		varying vec4 v_Normal;
		void main()
		{
			const vec4 bitShift = vec4(1.0, 256.0, 256.0*256.0, 256.0*256.0*256.0);
			const vec4 bitMask = vec4(1.0/256.0, 1.0/256.0, 1.0/256.0, 0.0);
			vec4 rgbaDepth = fract(gl_FragCoord.z*bitShift);
			rgbaDepth -= rgbaDepth.gbaa*bitMask;
			gl_FragColor = rgbaDepth;
		}
	</script>

	<script id="table-vertex-shader" type="x-shader/x-vertex">
		attribute vec4 a_Position;
		attribute vec2 a_TexCoord;
		uniform mat4 u_Perspective;
		uniform mat4 u_View;
		uniform mat4 u_Light_Perspective;
		uniform mat4 u_Light_View;

		varying vec4 v_PositionFromLight;
		varying vec2 vTexCoord;

		void main()
		{
			gl_Position = u_Perspective * u_View * a_Position;
			v_PositionFromLight = u_Light_Perspective * u_Light_View * a_Position;
			vTexCoord = a_TexCoord;
		}
	</script>
	
	<script id="table-fragment-shader" type="x-shader/x-fragment">
		precision highp float;
		uniform sampler2D shadowMap;
		uniform sampler2D marbletexMap;
		uniform float u_ambientIntensity;
		uniform vec2 texmapscale;
		varying vec4 v_PositionFromLight;
		varying vec2 vTexCoord;

		float unpackDepth(const in vec4 rgbaDepth) {
			const vec4 bitShift = vec4(1.0, 1.0/256.0, 1.0/(256.0*256.0), 1.0/(256.0*256.0*256.0));
			return dot(rgbaDepth, bitShift);
		}

		vec4 offset_lookup(sampler2D map, vec3 shadowCoord, vec2 offset) {
			return texture2D(map, shadowCoord.xy + offset*texmapscale);
		}

		void main()
		{
			vec3 shadowCoord = (v_PositionFromLight.xyz/v_PositionFromLight.w)/2.0 + 0.5;
			float finalVisibility = 0.0;
			float x, y;
			for (float y = -5.5; y <= 5.5; y += 1.0) {
				for (float x = -5.5; x <= 5.5; x += 1.0){
					vec4 rgbaDepth = offset_lookup(shadowMap, shadowCoord, vec2(x, y));
					float depth = unpackDepth(rgbaDepth);
					float visibility = (shadowCoord.z > depth + 0.0015) ? 0.0 : 1.0;
					finalVisibility += visibility;
				}
			}
			finalVisibility = finalVisibility / 144.0;
			
			vec3 L_a = vec3(u_ambientIntensity);
			gl_FragColor = texture2D(marbletexMap, vTexCoord);
			gl_FragColor.rgb = gl_FragColor.rgb * (finalVisibility + L_a);
		}
	</script>

	<script id="teapot-vertex-shader" type="x-shader/x-vertex">
		attribute vec4 a_Position;
		attribute vec4 a_Color;
		attribute vec3 a_Normal;

		uniform mat4 u_View;
		uniform mat4 u_Perspective;
		uniform mat4 u_Model;
		uniform mat4 u_Reflection;
		uniform mat3 u_NormalMatrix;
		uniform mat4 u_Light_Perspective;
		uniform mat4 u_Light_View;
		
		varying vec4 v_PositionFromLight;
		varying vec4 v_Color;
		varying vec3 v_Normal;
		varying vec3 v_Position;

		void main()
		{
			gl_Position = u_Perspective * u_View * u_Reflection * u_Model * a_Position;
			v_PositionFromLight = u_Light_Perspective * u_Light_View * u_Model * a_Position;
			v_Position = (u_Model * a_Position).xyz;
			v_Color = a_Color;		
			v_Normal = normalize(u_NormalMatrix*a_Normal);
		}
	</script>
	
	<script id="teapot-fragment-shader" type="x-shader/x-fragment">
		precision highp float;
		varying vec4 v_Color;
		varying vec3 v_Normal;
		varying vec3 v_Position;

		uniform float u_diffuseCoefficient;
		uniform float u_ambientIntensity;
		uniform float u_specularCoefficient;
		uniform float u_shininessCoefficient;
		uniform vec3 u_lightIntensity;
		uniform vec3 u_cameraPosition;
		uniform vec4 u_lightPosition;
		uniform vec2 texmapscale;

		uniform sampler2D shadowMap;
		varying vec4 v_PositionFromLight;

		float unpackDepth(const in vec4 rgbaDepth) {
			const vec4 bitShift = vec4(1.0, 1.0/256.0, 1.0/(256.0*256.0), 1.0/(256.0*256.0*256.0));
			return dot(rgbaDepth, bitShift);
		}

		vec4 offset_lookup(sampler2D map, vec3 shadowCoord, vec2 offset) {
			return texture2D(map, shadowCoord.xy + offset*texmapscale);
		}

		void main()
		{
			vec3 shadowCoord = (v_PositionFromLight.xyz/v_PositionFromLight.w)/2.0 + 0.5;
			float finalVisibility = 0.0;
			float x, y;
			for (float y = -5.5; y <= 5.5; y += 1.0) {
				for (float x = -5.5; x <= 5.5; x += 1.0){
					vec4 rgbaDepth = offset_lookup(shadowMap, shadowCoord, vec2(x, y));
					float depth = unpackDepth(rgbaDepth);
					float visibility = (shadowCoord.z > depth + 0.0015) ? 0.0 : 1.0;
					finalVisibility += visibility;
				}
			}
			finalVisibility = finalVisibility / 144.0;

			vec3 n_Normal = normalize(v_Normal);
			vec3 w_i = normalize(u_lightPosition.xyz - v_Position);
			vec3 L_i = u_lightIntensity/pow(length(u_lightPosition.xyz - v_Position), 2.0);
			vec3 L_rd = u_diffuseCoefficient * v_Color.rgb * L_i * max(dot(n_Normal, w_i), 0.0);
			vec3 L_a = vec3(u_ambientIntensity);
			vec3 w_o = normalize(u_cameraPosition - v_Position);
			vec3 w_h = normalize(w_i + w_o);
			vec3 L_o = L_rd + u_specularCoefficient * L_i * pow(max(dot(n_Normal, w_h), 0.0), u_shininessCoefficient);
			
			gl_FragColor.rgb = L_o * finalVisibility + L_a * u_diffuseCoefficient;
			gl_FragColor.a = 1.0;
		}
	</script>
</head>
<body>
	<canvas id="gl-canvas" width="512" height="512">Please use a browser that supports WebGL</canvas>
	<button id="toggleRotation">Toggle Light Rotation</button>
	<button id="toggleMovement">Toggle Teapot Movement</button>
	<div>
		Light Intensity (I): 0.0 <input id="lightIntensity" type="range"
		min="0.0" max="10.0" step="0.5" value="8.0" />
		10.0
	</div>
	<div>
		Ambient Intensity (L_a): 0.0<input id="ambientIntensity" type="range"
		min="0.0" max="1.0" step="0.1" value="0.1" />
		1.0
	</div>
	<div>
		Diffuse coefficient (k_d): 0.0 <input id="diffuseCoefficient" type="range"
		min="0.0" max="1.0" step="0.1" value="1.0" />
		1.0
	</div>
	<div>
		Specular coefficient (k_s): 0.0 <input id="specularCoefficient" type="range"
		min="0.0" max="1.0" step="0.1" value="0.5" />
		1.0
	</div>
	<div>
		Shininess (s): 0.0 <input id="shininessCoefficient" type="range"
		min="0.0" max="1000.0" step="10.0" value="500.0" />
		1.0e3
	</div>
</body>
</html>
